10 SCREEN 0,1,1:CLS:LOCATE,,0:CONSOLE,,0

100 LOCATE 14,7:ATTRB0,1:?"M O O Y A N":ATTRB0,0:LOCATE8,10:?"THOMSON MO6 POOYAN CLONE":LOCATE10,11:?"* WORK IN PROGRESS *":LOCATE2,13:?"GRAPHICS AND CODE by Giuseppe Stassi"
105 LOCATE2,14:?"CODE OPTIMIZATION by Fabrizio Caruso":LOCATE7,16:?"100% written in BASIC 128":LOCATE10,19:?"PRESS THE SPACE BAR"
110 DO:A$=INKEY$:IF A$=" " THEN EXIT
115 LOOP

120 PALETTE 0,3856
125 PALETTE 1,256
130 PALETTE 2,256
135 PALETTE 3,256
140 PALETTE 4,256
145 PALETTE 5,256
150 PALETTE 6,256
155 PALETTE 7,256
160 PALETTE 8,256
165 PALETTE 9,256
170 PALETTE 10,256
175 PALETTE 11,256
180 PALETTE 12,256
185 PALETTE 13,256
190 PALETTE 14,256
195 PALETTE 15,256

200 CLS:SCREEN 0,1,0:LOCATE14,12:?"PLEASE WAIT"
205 DIM BG%(1716):LOADP"CASS:BG",BG%(1716)
210 DIM S%(2500):LOADP"CASS:SP",S%(2500)
215 PALETTE 0,256:BOXF(4,12)-(24,13)" ",1:PUT(0,0),BG%(1716):PUT(32,0),S%(2500)

220 DEFINT A-Z
225 GET(25,4)-(26,8),S%(2500):REM HERO
230 S1=S%(2500):GET(35,1)-(36,2),S%(S1):REM HERO FRECCIA
235 S2=S%(S1):GET(32,3)-(36,4),S%(S2):REM TIRA CORDA 1
240 S3=S%(S2):GET(26,3)-(30,4),S%(S3):REM TIRA CORDA 2
245 S4=S%(S3):GET(32,5)-(33,8),S%(S4):REM LUPO ROSSO
250 BGR=S%(S4):GET(0,0)-(1,0),S%(BGR)
255 BGC=S%(BGR):GET(0,0)-(0,1),S%(BGC)
260 BGB=S%(BGC):GET(0,0)-(1,1),S%(BGB)
265 S5=S%(BGB):GET(34,14)-(35,15),S%(S5):REM WALK 1
270 S6=S%(S5):GET(36,14)-(37,15),S%(S6):REM WALK 2
275 S7=S%(S6):GET(32,14)-(33,15),S%(S7):REM WALK 3
280 WW(0)=S5:WW(1)=S7:WW(2)=S5:WW(3)=S6 
285 S8=S%(S7):GET(36,5)-(37,6),S%(S8):REM PALLONCINO COLPITO
290 S9=S%(S8):GET(36,7)-(37,8),S%(S9):REM CADE
295 S10=S%(S9):GET(32,16)-(33,17),S%(S10):REM LUPO SCALA 1
300 S11=S%(S10):GET(34,16)-(35,17),S%(S11):REM LUPO SCALA 2
305 S12=S%(S11):GET(36,16)-(37,17),S%(S12):REM LUPO SCALA 3
310 S13=S%(S12):GET(38,16)-(39,17),S%(S13):REM LUPO SCALA 4
315 SS(0)=S10:SS(1)=S11:SS(2)=S12:SS(3)=S13 
320 S14=S%(S13):GET(34,6)-(35,8),S%(S14):REM LUPO ROSSO PALLONCINO PICCOLO
325 S15=S%(S14):GET(27,8)-(28,9),S%(S15):REM SCALA
330 S16=S%(S15):GET(38,14)-(39,15),S%(S16):REM LUPO RIGHT
335 S17=S%(S16):GET(37,1)-(39,2),S%(S17):REM PORCO MORTO
340 S18=S%(S17):GET(33,1)-(34,2),S%(S18):REM FRAGOLA
345 S19=S%(S18):GET(34,5)-(35,5),S%(S19):REM 100
350 S20=S%(S19):GET(38,0)-(39,0),S%(S20):REM 200
355 S21=S%(S20):GET(32,23)-(33,24),S%(S21):REM MELA

400 Y=4:Y2=5:X2=11:ST5=1:XS5MB=11:NF1=0:NF2=0:C=0:UP=0:XUPS=27:R=1+INT(RND*2):YS18=5:XS18=19:FRU=0:PLAY"L1T5DO"
405 PALETTE 0,0:BOXF(32,0)-(39,24)" ",0,0
410 LIVES=3:LOCATE 33,0,0:COLOR 4,0:?"LIVES:":LOCATE 32,1,0:?LIVES
415 SCORE=0:LOCATE 33,3,0:COLOR 11,0:?"SCORE:":LOCATE 32,4,0:?SCORE
420 WO=15:LOCATE 33,6,0:COLOR 2,0:?"WOLVES:":LOCATE 32,7,0:?WO
425 LEVEL=1:LOCATE 28,1,0:COLOR 4,1:?LEVEL
430 POKE &H2019,PEEK(&H2019) OR 8:GOTO 900

500 ON INTERVAL = 5 GOSUB 700
505 INTERVAL ON

510 DO
515 IF STICK(0)=1 THEN IF Y > 4 THEN Y=Y-1:PUT(25,Y),S%(2500):COLOR 10,1:BOX(25,Y+5)-(26,Y+5)" ",1,1:IF ST5=1 THEN PUT(26,3),S%(S2):ST1=1:ST5=0
520 IF STICK(0)=5 THEN IF Y < 14 THEN Y=Y+1:PUT(25,Y),S%(2500):IF ST1=1 THEN PUT(26,3),S%(S3):ST5=1:ST1=0
525 IF STRIG(0) THEN PLAY"L1T1A2DOMI":PUT(25,Y+2),S%(S1):SHIELD=1+INT(RND*3):GOSUB 600
530 LOOP

600 XF=23*8:YF=(Y*8)+29:YFIRE=YF@8
605 FOR I=1 TO 14
610 PSET(XF,YF),-16:XF=XF-8:PSET(XF+8,YF),-2
615 IF XF@8=X2 THEN IF YFIRE=Y2 OR YFIRE=Y2+1 THEN IF SHIELD<>1 THEN PUT(X2,Y2),S%(S8):PLAY"L1T5DO":EKO=1:Y2C=Y2+1:WOSTRIKE=1:SCORE=SCORE+100:COLOR 11,0:LOCATE 32,4,0:?SCORE:EXIT ELSE IF SHIELD=1 THEN PUT(X2,Y2+2),S%(S9):PLAY"L1T5A0MI":EKO=0:EXIT
620 IF XF@8=XS18 THEN IF YFIRE=YS18 OR YFIRE=YS18+1 THEN FRUSTRIKE=1:SCORE=SCORE+200:COLOR 11,0:LOCATE 32,4,0:?SCORE:PLAY"L1T5O5A100MIRESI":EXIT 
625 NEXT
630 PUT(25,Y),S%(2500)
635 RETURN

700 REM
705 IF STA=0 OR WO > 1 THEN IF XS5MT<10 THEN XS5MT=XS5MT+2:NF0=NF0+1:PUT(XS5MT,1),S%(WW(NF0 AND 3)):PUT(XS5MT-2,1),S%(BGB):LANCIO=0
710 IF XS5MT=10 THEN STA=1:IF LANCIO=0 THEN PUT(10,1),S%(BGC):PUT(11,0),S%(S14)
715 IF EKO=1 THEN YKO=YKO+2:PUT(X2,YKO),S%(S9):PUT(X2,YKO-2),S%(BGB):PUT(X2+2,Y2C),S%(BGR):Y2=5 ELSE IF STA=1 THEN Y2=Y2+1:PUT(X2,Y2),S%(S4):PUT(X2,Y2-1),S%(BGR):YKO=Y2:LANCIO=1
720 IF Y2=6 THEN PUT(11,1),S%(BGB):PUT(11,0),S%(BGR):XS5MT=0
725 REM ******** Atterraggio lupo ********
730 IF Y2 > 18 THEN PUT(X2,19),S%(BGB):PUT(X2,21),S%(S5):STARTWALK=1:Y2=5:WO=WO-1:RETURN
735 IF STARTWALK=1 THEN NF1=NF1+1:XS5MB=XS5MB+1:PUT(XS5MB,21),S%(WW(NF1 AND 3)):PUT(XS5MB-1,21),S%(BGC) 
740 IF XS5MB = 23 THEN PUT(23,21),S%(BGB):XS5MB=X2:STARTWALK=0:IF UP < 5 THEN UP=UP+1:C=1
745 REM ******** Lupi sulle scale ********
750 IF UP<4 THEN IF C=1 THEN YUPS=20-4*UP:C=0:PUT(XUPS,YUPS),S%(SS(NF2 AND 3)):NF2=NF2+1:C=1 
755 IF NF2=4 THEN C=0:NF2=0 
760 IF UP=4 THEN IF LIVES > 0 THEN GOTO 1000
765 IF WO=0 THEN GOTO 1100
770 IF LIVES=0 THEN IF UP=4 THEN GOTO 1200
775 IF FRU=0 THEN FRURAN=1+INT(RND*20):FRU=1
780 IF YS18=5 THEN IF FRUSTRIKE=1 THEN PUT(XS18+2,YS18C),S%(BGR):FRUSTRIKE=0
786 IF FRURAN=5 THEN YS18=YS18+2:YS18C=YS18:PUT(XS18,YS18),S%(S21):PUT(XS18,YS18-2),S%(BGB) ELSE FRU=0:FRURAN=0 
790 IF YS18>19 THEN PUT(XS18,19),S%(BGB):PUT(XS18,21),S%(BGB):YS18=5:FRU=0:FRURAN=0
795 IF FRUSTRIKE=1 THEN PUT(XS18,YS18C),S%(BGB):PUT(XS18+2,YS18C),S%(S20):YS18=5:FRU=0:FRURAN=0
800 IF WOSTRIKE=1 THEN PUT(X2+2,Y2C),S%(S19):WOSTRIKE=0
805 IF YKO>19 THEN PUT(X2,19),S%(BGB):PUT(X2,21),S%(BGB):EKO=0:PUT(X2+2,Y2C),S%(BGR):Y2=5:WO=WO-1:COLOR 2,0:LOCATE 32,7,0:?WO:LOCATE 32,4,0
810 REM
820 RETURN

900 PALETTE 0,0
905 PALETTE 1,256
910 PALETTE 2,48
915 PALETTE 3,147
920 PALETTE 4,255
925 PALETTE 5,143
930 PALETTE 6,59
935 PALETTE 7,22
940 PALETTE 8,2
945 PALETTE 9,3744
950 PALETTE 10,3856
955 PALETTE 11,3343
960 PALETTE 12,15
965 PALETTE 13,6
970 PALETTE 14,3999
975 PALETTE 15,3817
980 GOTO 500
 
1000 INTERVAL OFF:PLAY"DO":PUT(29,3),S%(S16):PLAY"SODOSODO":FOR I=Y TO 14:PUT(25,I),S%(2500):NEXT:PUT(25,16),S%(BGB):PUT(21,21),S%(S17)
1005 LIVES=LIVES-1:COLOR 4,0:LOCATE 32,1,0:?LIVES:PUT(X2,Y2),S%(BGB):PUT(X2,Y2+2),S%(BGB):PUT(XS5MB,21),S%(BGB):PUT(XS5MT+1,1),S%(BGB):PUT(XS5MT+1,0),S%(BGR)
1010 X2=11:XS5MB=11:NF1=0:NF2=0:C=0:XS5MT=0:Y2=5:STA=0:ST5=1:X2=11:Y=4:STARTWALK=0:WO=WO+UP:UP=0:EKO=0
1015 COLOR 4,1:LOCATE10,12:?"PRESS SPACE"
1020 DO:A$=INKEY$:IF A$=" " THEN EXIT
1025 LOOP:COLOR 2,0:LOCATE 32,7,0:?WO
1030 BOXF(4,12)-(24,13)" ",1,1:BOXF(25,4)-(26,18)" ",1,1:BOXF(21,21)-(23,22)" ",1,1:PUT(25,4),S%(2500)
1035 PUT(26,3),S%(S2):PUT(25,4),S%(2500):PUT(27,8),S%(S15):PUT(27,12),S%(S15):PUT(27,16),S%(S15)
1040 INTERVAL ON:GOTO 700

1100 INTERVAL OFF:PLAY"DO"
1105 LEVEL=LEVEL+1:LOCATE 28,1,0:COLOR 4,1:?LEVEL:PUT(X2,Y2),S%(BGB):PUT(X2,Y2+2),S%(BGB):PUT(XS5MB,21),S%(BGB):PUT(XS5MT+1,1),S%(BGB):PUT(XS5MT+1,0),S%(BGR)
1110 X2=11:XS5MB=11:NF1=0:NF2=0:C=0:XS5MT=0:Y2=5:STA=0:UP=0:ST5=1:X2=11:Y=4:STARTWALK=0:WO=15*LEVEL:EKO=0
1115 COLOR 4,1:LOCATE8,12:?"LEVEL COMPLETED":LOCATE10,13:?"PRESS SPACE"
1120 DO:A$=INKEY$:IF A$=" " THEN EXIT
1125 LOOP:COLOR 2,0:LOCATE 32,7,0:?WO:
1130 BOXF(4,12)-(24,13)" ",1,1:BOXF(4,12)-(24,13)" ",1,1:BOXF(25,4)-(26,18)" ",1,1:PUT(25,4),S%(2500)
1135 PUT(27,8),S%(S15):PUT(27,12),S%(S15):PUT(27,16),S%(S15):INTERVAL ON:GOTO 700

1200 INTERVAL OFF:PLAY"DO":PUT(29,3),S%(S16):PLAY"SODOSODO":FOR I=Y TO 14:PUT(25,I),S%(2500):NEXT:PUT(25,16),S%(BGB):PUT(21,21),S%(S17)
1205 PUT(X2,Y2),S%(BGB):PUT(X2,Y2+2),S%(BGB):PUT(XS5MB,21),S%(BGB):PUT(XS5MT+1,1),S%(BGB):PUT(XS5MT+1,0),S%(BGR)
1210 X2=11:XS5MB=11:NF1=0:NF2=0:C=0:XS5MT=0:Y2=5:STA=0:UP=0:ST5=1:X2=11:Y=4:STARTWALK=0:LIVES=3:LEVEL=1:WO=15:SCORE=0:EKO=0
1215 COLOR 4,1:LOCATE11,12:?"GAME OVER":LOCATE 10,13,0:?"PRESS SPACE"
1220 DO:A$=INKEY$:IF A$=" " THEN EXIT
1225 LOOP
1230 BOXF(4,12)-(24,13)" ",1,1:BOXF(25,4)-(26,18)" ",1,1:BOXF(21,21)-(23,22)" ",1,1:PUT(25,4),S%(2500)
1235 PUT(26,3),S%(S2):PUT(25,4),S%(2500):PUT(27,8),S%(S15):PUT(27,12),S%(S15):PUT(27,16),S%(S15)
1240 COLOR 4,0:LOCATE 32,1,0:?LIVES:LOCATE 28,1,0:COLOR 4,1:?LEVEL:COLOR 2,0:LOCATE 32,7,0:?WO
1245 INTERVAL ON:GOTO 700